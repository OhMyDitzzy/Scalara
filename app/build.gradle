plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.oss.license.plugin)
}

def isRunningOnCI = System.getenv("CI") == "true"
def versionNameFromCI = System.getenv("VERSION_NAME") ?: "dev"
def versionCodeFromCI = System.getenv("VERSION_CODE")?.toInteger() ?: 1

android {
    namespace 'com.ditzdev.scalara'
    compileSdk 36

    defaultConfig {
        applicationId 'com.ditzdev.scalara'
        minSdk 26
        targetSdk 35
        versionCode versionCodeFromCI
        versionName versionNameFromCI

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            if (isRunningOnCI) {
                storeFile file(System.getenv("KEYSTORE_PATH") ?: "release.keystore")
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '-debug'
            debuggable true
            minifyEnabled false
        }
        
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (isRunningOnCI) {
                signingConfig signingConfigs.release
            }
        }
    }
    
    lint {
        abortOnError true
        checkReleaseBuilds false
        warningsAsErrors true
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation project(':ui')
    implementation libs.androidx.core
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintlayout
    implementation libs.shizuku.api
    implementation libs.shizuku.provider
    implementation libs.oss.license
}